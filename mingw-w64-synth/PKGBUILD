# Maintainer: iamwacko <iamawacko@protonmail.com>

_realname=synth
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.6.9
pkgrel=1
pkgdesc="The Open Source Data Generator"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.getsynth.com/'
license=(spdx:Apache-2.0)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/shuttle-hq/${_realname}/archive/refs/targs/v${pkgver}.tar.gz")
sha256sums=('18996ebb6a7a8a9d36d961fd7fc1dee38d78b0d03c57ab5681f312600ab5139e')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOTSTRAP=1

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOSTRAP=1

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOTSTRAP=1

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
