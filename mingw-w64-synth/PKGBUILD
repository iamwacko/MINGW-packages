# Maintainer: iamwacko <iamawacko@protonmail.com>

_realname=synth
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.6.9
pkgrel=1
pkgdesc="The Open Source Data Generator"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.getsynth.com/'
license=(spdx:Apache-2.0)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "${MINGW_PACKAGE_PREFIX}-sqlite3")
source=("https://github.com/shuttle-hq/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('f89fc355dc0e311a6e5e0e8047f4721f23e35cdcb3355e85c7c66e8e2e67da91')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOTSTRAP=1

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOSTRAP=1

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export CARGO_TARGET_DIR=target
  export RUSTC_BOOTSTRAP=1

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
